plugins {
    id 'com.android.library'
    id 'maven-publish'
}

def versionMajor = 1
def versionMinor = 4
def versionPatch = 2
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

android {
    namespace 'com.heinrichreimersoftware.androidissuereporter'

    compileSdk 34
    defaultConfig {
        minSdk 15
        targetSdk 34
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}" + (versionPatch == 0 ? "" : ".${versionPatch}")
        vectorDrawables.useSupportLibrary = true
    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'

    implementation 'org.kohsuke:github-api:1.318'
    implementation 'com.github.AAkira:ExpandableLayout:v1.5.0@aar'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.findByName('release')
            }
        }
    }
}
